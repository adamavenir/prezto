#
# Exposes information about the Node.js environment via the $node_info
# associative array.
#
# Authors:
#   Zeh Rizzatti <zehrizzatti@gmail.com>
#

local version
local version_format
local version_formatted

unset node_info
typeset -gA node_info

if (( $+commands[nodenv] )); then
  version="${${$(nodenv version)#v}[(w)0]}"
elif (( $+commands[volta] )); then
  # Parse Volta output format: "runtime node@22.12.0 (default)"
  version="${${$(volta list node | grep 'default')#*@}%% *}"
elif (( $+functions[nvm_version] )); then
  version="${$(nvm_version)#v}"
elif (( $+commands[node] )) ; then
  version="${$(node -v)#v}"
fi

if [[ "$version" != (none|system) ]]; then
  zstyle -s ':prezto:module:node:info:version' format 'version_format'
  zformat -f version_formatted "$version_format" "v:$version"
  node_info[version]="$version_formatted"
fi